/*
 *  Copyright 2010-2014 Fabric Software Inc. All rights reserved.
 */

object RGBAttribute : GeometryAttribute, StatisticSourceWithAutoRegisterMember {
  UInt32 version;
  String name;
  UInt32 containerIndex;

  RGB values[];
  StatisticsAutoRegisterMember autoStats;
};

function RGBAttribute() {
  if(FabricStatisticsEnabled())
    this.autoStats = StatisticsAutoRegisterMember(this);
}

//Implementation of StatisticSource interface's 'getStatistics()'.
function StatisticRecord[] RGBAttribute.getStatistics() {
  StatisticRecord stats[];
  stats.push( StatisticRecord( Statistic_Name, this.name ) );
  stats.push( StatisticRecord( Statistic_Category, "Attribute" ) );
  stats.push( StatisticRecord( Statistic_MemoryUsage, this.values.size()*3 ) );
  return stats;
}

inline String RGBAttribute.getName() {
  return this.name;
}

inline UInt32 RGBAttribute.getVersion() {
  return this.version;
}

inline Size RGBAttribute.getContainerIndex() {
  return this.containerIndex;
}

inline RGBAttribute.setContainerIndex!( Size index ) {
  this.containerIndex = index;
}

function RGBAttribute RGBAttribute.clone() {
  //The difference with default clone() is that new.base.autoStat will point to 'new' (from constructor)
  RGBAttribute new = RGBAttribute();
  new.version = this.version;
  new.name = this.name.clone();
  new.containerIndex = this.containerIndex;
  new.values = this.values.clone();
  return new;
}

function Ref<StatisticSource> RGBAttribute.getStatisticsSource() {
  return this.autoStats;
}

inline RGBAttribute.resize!(Size count) {
  this.values.resize(count);
  ++this.version;
}

inline Size RGBAttribute.size() {
  return this.values.size();
}

inline Data RGBAttribute.data() {
  Ref<RGBAttribute> thisRef = this;
  return thisRef.values.data();
}

//Make sure to call this method after position changes are finished, so caches (such as drawing data) know that they need to be regenerated.
inline RGBAttribute.incrementVersion!() {
  ++this.version;
}

inline RGBAttribute.copyValue!( Size sourceIndex, Size targetIndex ) {
  this.values[targetIndex] = this.values[sourceIndex];
}

inline RGBAttribute.copyValue!( Ref<Object> sourceAttr, Size sourceIndex, Size targetIndex ) {
  Ref<RGBAttribute> source = sourceAttr;
  this.values[targetIndex] = source.values[sourceIndex];
}

inline RGB RGBAttribute.getLinearCombination( LocalL16UInt32Array sourceIndices, LocalL16ScalarArray sourceWeights ) {
  Vec3 result;
  for( Size i = 0; i < sourceIndices.size(); ++i ) {
    RGB sourceData = this.values[sourceIndices.get(i)];
    Vec3 source = Vec3(sourceData.r, sourceData.g, sourceData.b);
    result += source*sourceWeights.get(i);
  }
  result = result*255.0 + Vec3(0.5,0.5,0.5);//rounding
  return RGB(Byte(result.x), Byte(result.y), Byte(result.z));
}

inline RGBAttribute.linearCombine!( Ref<Object> sourceAttr, LocalL16UInt32Array sourceIndices, LocalL16ScalarArray sourceWeights, Size targetIndex ) {
  Ref<RGBAttribute> source = sourceAttr;
  this.values[targetIndex] = source.getLinearCombination(sourceIndices, sourceWeights);
}

inline RGBAttribute.copyFrom!( Ref<Object> otherAttr ) {
  Ref<RGBAttribute> other = otherAttr;
  this.incrementVersion();

  // Copy in place to avoid mallocs
  Size size = other.values.size();
  this.values.resize( size );
  for( Size i = 0; i < size; ++i )
    this.values[i] = other.values[i];
}

inline RGBAttribute.setFromScalar!( Size index, LocalL16ScalarArray value ) {
  this.values[index].r = Byte(value.get(0)*255.0 + 0.5);//rounding
  this.values[index].g = Byte(value.get(1)*255.0 + 0.5);//rounding
  this.values[index].b = Byte(value.get(2)*255.0 + 0.5);//rounding
}

inline Size RGBAttribute.getScalarValueSize() {
  return 3;
}

inline Boolean RGBAttribute.equalValues( Size index1, Size index2 ) {
  return this.values[index1].equal( this.values[index2] );
}

function String RGBAttribute.valueDesc( Size index ) {
  return String(this.values[index]);
}

require Util;
function String RGBAttribute.unitTestPrint( Size index ) {
  return this.valueDesc(index);
}
//Returns a string description of the attribute.
function String RGBAttribute.getDesc?( Boolean includeValues) {
  String desc;
  desc += "Attribute: " + this.getName() + " nb elements = "+ this.values.size()+'\n';
  if(includeValues){
    if(this.values.size > 0){
      desc += "  values:[ \n";
      for(Integer i=0; i<this.values.size; i++){
        desc += '    ' + unitTestPrint(this.values[i]) + " \n";
      }
      desc += "  ]\n";
    }
    else{
      desc += "  values:[]\n";
    }
  }
  return desc;
}
function String RGBAttribute.getDesc?() {
  return this.getDesc(false);
}

function Ref<RGBAttribute> GeometryAttributes.getOrCreateRGBAttribute!( String name ) {
  Ref<GeometryAttribute> attr = this.getAttribute( name, RGBAttribute );
  if( attr )
    return attr;

  RGBAttribute newAttr = RGBAttribute();
  newAttr.resize(this.elementCount);
  newAttr.name = name;
  newAttr.version = this.keysVersion;
  this.attachNewAttribute(newAttr);
  return newAttr;
}
