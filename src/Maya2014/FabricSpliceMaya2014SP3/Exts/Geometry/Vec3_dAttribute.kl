/*
 *  Copyright 2010-2014 Fabric Software Inc. All rights reserved.
 */

object Vec3_dAttribute : GeometryAttribute, StatisticSourceWithAutoRegisterMember {
  UInt32 version;
  String name;
  UInt32 containerIndex;

  Vec3_d values[];
  Boolean normalize;//SetUnit after linearCombine
  StatisticsAutoRegisterMember autoStats;
};

function Vec3_dAttribute() {
  if(FabricStatisticsEnabled())
    this.autoStats = StatisticsAutoRegisterMember(this);
}

//Implementation of StatisticSource interface's 'getStatistics()'.
function StatisticRecord[] Vec3_dAttribute.getStatistics() {
  StatisticRecord stats[];
  stats.push( StatisticRecord( Statistic_Name, this.name ) );
  stats.push( StatisticRecord( Statistic_Category, "Attribute" ) );
  stats.push( StatisticRecord( Statistic_MemoryUsage, this.values.size()*3*8 ) );
  return stats;
}

inline String Vec3_dAttribute.getName() {
  return this.name;
}

inline UInt32 Vec3_dAttribute.getVersion() {
  return this.version;
}

inline Size Vec3_dAttribute.getContainerIndex() {
  return this.containerIndex;
}

inline Vec3_dAttribute.setContainerIndex!( Size index ) {
  this.containerIndex = index;
}

function Vec3_dAttribute Vec3_dAttribute.clone() {
  //The difference with default clone() is that new.base.autoStat will point to 'new' (from constructor)
  Vec3_dAttribute new = Vec3_dAttribute();
  new.version = this.version;
  new.name = this.name.clone();
  new.containerIndex = this.containerIndex;
  new.values = this.values.clone();
  new.normalize = this.normalize;
  return new;
}

function Ref<StatisticSource> Vec3_dAttribute.getStatisticsSource() {
  return this.autoStats;
}

inline Vec3_dAttribute.resize!(Size count) {
  this.values.resize(count);
  ++this.version;
}

inline Size Vec3_dAttribute.size() {
  return this.values.size();
}

inline Data Vec3_dAttribute.data() {
  Ref<Vec3_dAttribute> thisRef = this;
  return thisRef.values.data();
}

//Make sure to call this method after position changes are finished, so caches (such as drawing data) know that they need to be regenerated.
inline Vec3_dAttribute.incrementVersion!() {
  ++this.version;
}

inline Vec3_dAttribute.copyValue!( Size sourceIndex, Size targetIndex ) {
  this.values[targetIndex] = this.values[sourceIndex];
}

inline Vec3_dAttribute.copyValue!( Ref<Object> sourceAttr, Size sourceIndex, Size targetIndex ) {
  Ref<Vec3_dAttribute> source = sourceAttr;
  this.values[targetIndex] = source.values[sourceIndex];
}

inline Vec3_d Vec3_dAttribute.getLinearCombination( LocalL16UInt32Array sourceIndices, LocalL16ScalarArray sourceWeights ) {
  Vec3_d result;
  for( Size i = 0; i < sourceIndices.size(); ++i )
    result += this.values[sourceIndices.get(i)]*sourceWeights.get(i);
  if(this.normalize)
    result = result.unit_safe();
  return result;
}

inline Vec3_dAttribute.linearCombine!( Ref<Object> sourceAttr, LocalL16UInt32Array sourceIndices, LocalL16ScalarArray sourceWeights, Size targetIndex ) {
  Ref<Vec3_dAttribute> source = sourceAttr;
  this.values[targetIndex] = source.getLinearCombination( sourceIndices, sourceWeights );
}

inline Vec3_dAttribute.copyFrom!( Ref<Object> otherAttr ) {
  Ref<Vec3_dAttribute> other = otherAttr;
  this.incrementVersion();

  // Copy in place to avoid mallocs
  Size size = other.values.size();
  this.values.resize( size );
  for( Size i = 0; i < size; ++i )
    this.values[i] = other.values[i];
}

inline Vec3_dAttribute.setFromScalar!( Size index, LocalL16ScalarArray value ) {
  this.values[index].x = value.get(0);
  this.values[index].y = value.get(1);
  this.values[index].z = value.get(2);
}

inline Size Vec3_dAttribute.getScalarValueSize() {
  return 3;
}

inline Boolean Vec3_dAttribute.equalValues( Size index1, Size index2 ) {
  return this.values[index1].equal( this.values[index2] );
}

function String Vec3_dAttribute.valueDesc( Size index ) {
  return String(this.values[index]);
}

require Util;
function String Vec3_dAttribute.unitTestPrint( Size index ) {
  return unitTestPrint(this.values[index]);
}
//Returns a string description of the attribute.
function String Vec3_dAttribute.getDesc?( Boolean includeValues) {
  String desc;
  desc += "Attribute: " + this.getName() + " nb elements = "+ this.values.size()+'\n';
  if(includeValues){
    if(this.values.size > 0){
      desc += "  values:[ \n";
      for(Integer i=0; i<this.values.size; i++){
        desc += '    ' + unitTestPrint(this.values[i]) + " \n";
      }
      desc += "  ]\n";
    }
    else{
      desc += "  values:[]\n";
    }
  }
  return desc;
}
function String Vec3_dAttribute.getDesc?() {
  return this.getDesc(false);
}


function Ref<Vec3_dAttribute> GeometryAttributes.getOrCreateVec3_dAttribute!( String name ) {
  Ref<GeometryAttribute> attr = this.getAttribute( name, Vec3_dAttribute );
  if( attr )
    return attr;

  Vec3_dAttribute newAttr = Vec3_dAttribute();
  newAttr.resize(this.elementCount);
  newAttr.name = name;
  newAttr.version = this.keysVersion;
  this.attachNewAttribute(newAttr);
  return newAttr;
}
