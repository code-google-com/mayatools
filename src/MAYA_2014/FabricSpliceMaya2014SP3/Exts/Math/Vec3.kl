
//
// Copyright 2010-2014 Fabric Software Inc. All rights reserved.
//
/*SPHINX-description
The :ref:`vec3` type represents a vector with 3 components. The :ref:`vec3` is also used as the row type for the :ref:`mat33`.

Example
---------

.. code-block:: js

    Vec3 v(0.0, 0.0, 0.0);
*/

struct Vec3
{
  Scalar x;
  Scalar y;
  Scalar z;
};


// constructor from scalar components
inline Vec3(Scalar x, Scalar y, Scalar z) {
  this.set(x, y, z);
}

// setter from scalar components
inline Vec3 Vec3.set!(Scalar x, Scalar y, Scalar z) {
  this.x = x;
  this.y = y;
  this.z = z;
  return this;
}

// setter from another vector
inline Vec3 Vec3.set!(Vec3 v) {
  this.x = v.x;
  this.y = v.y;
  this.z = v.z;
  return this;
}

// setter from (standard/external) float32 array and an offset
inline Vec3.set!(Float32 v<>, UInt32 o) {
  if(v.size() >= 3 + o) {
    this.x = v[o + 0];
    this.y = v[o + 1];
    this.z = v[o + 2];
  }
}

// getter as external float32 array and an offset
inline Vec3.get(io Float32 v<>, UInt32 o) {
  if(v.size() >= 3 + o) {
    v[o + 0] = this.x;
    v[o + 1] = this.y;
    v[o + 2] = this.z;
  }
}

// getter as float32 array and an offset
inline Vec3.get(io Float32 v[], UInt32 o) {
  if(v.size() < 3 + o)
    v.resize(3 + o);
  v[o + 0] = this.x;
  v[o + 1] = this.y;
  v[o + 2] = this.z;
}

// setter from (standard/external) float64 array and an offset
inline Vec3.set!(Float64 v<>, UInt32 o) {
  if(v.size() >= 3 + o) {
    this.x = Float32(v[o + 0]);
    this.y = Float32(v[o + 1]);
    this.z = Float32(v[o + 2]);
  }
}

// getter as external float64 array and an offset
inline Vec3.get(io Float64 v<>, UInt32 o) {
  if(v.size() >= 3 + o) {
    v[o + 0] = this.x;
    v[o + 1] = this.y;
    v[o + 2] = this.z;
  }
}

// getter as float64 array and an offset
inline Vec3.get(io Float64 v[], UInt32 o) {
  if(v.size() < 3 + o)
    v.resize(3 + o);
  v[o + 0] = this.x;
  v[o + 1] = this.y;
  v[o + 2] = this.z;
}

// setter from (standard/external) float32 array
inline Vec3.set!(Float32 v<>) {
  this.set(v, 0);
}

// getter as external float32 array
inline Vec3.get(io Float32 v<>) {
  this.get(v, 0);
}

// getter as float32 array
inline Vec3.get(io Float32 v[]) {
  this.get(v, 0);
}

// setter from (standard/external) float64 array
inline Vec3.set!(Float64 v<>) {
  this.set(v, 0);
}

// getter as external float64 array
inline Vec3.get(io Float64 v<>) {
  this.get(v, 0);
}

// getter as float64 array
inline Vec3.get(io Float64 v[]) {
  this.get(v, 0);
}

// sets all components of this vec to 0.0
inline Vec3.setNull!() {
  this.x = this.y = this.z = 0.0;
}

// returns true if this vector is the same as another one
inline Boolean Vec3.equal(in Vec3 other) {
  return this.x == other.x && this.y == other.y && this.z == other.z;
}

// returns true if this vector is the same as another one
// (given a precision)
inline Boolean Vec3.almostEqual(in Vec3 other, in Scalar precision) {
  return
      (abs(this.x - other.x) < precision) &&
      (abs(this.y - other.y) < precision) &&
      (abs(this.z - other.z) < precision);
}

// returns true if this vector is the same as another one
// (using a default precision)
inline Boolean Vec3.almostEqual(in Vec3 other) {
  return this.almostEqual(other, PRECISION);
}

// returns the component of this vector by index
inline Scalar Vec3.component( Size i ) {
  if(i == 0)
    return this.x;
  else if(i == 1)
    return this.y;
  else
    return this.z;
}

// sets the component of this vector by index
inline Vec3.setComponent!( Size i, Scalar v ) {
  if(i == 0)
    this.x = v;
  else if(i == 1)
    this.y = v;
  else
    this.z = v;
}

// equals operator
inline Boolean == (Vec3 a, Vec3 b) {
  return a.x == b.x && a.y == b.y && a.z == b.z;
}

// not equals operator
inline Boolean != (Vec3 a, Vec3 b) {
  return a.x != b.x || a.y != b.y || a.z != b.z;
}

// unary -
inline Vec3 -Vec3() {
  return Vec3(-this.x, -this.y, -this.z);
}

// adds to vectors
inline Vec3 + (Vec3 a, Vec3 b) {
  return vecAdd(a, b);
}

// adds a vector to this one
inline Vec3. += (Vec3 other) {
  this = this + other;
}

// subtracts two vectors
inline Vec3 - (Vec3 a, Vec3 b) {
  return vecSub(a, b);
}

// subtracts a vector from this one
inline Vec3. -= (Vec3 other) {
  this = this - other;
}

// multiplies a scalar and a vector
inline Vec3 * (Scalar a, Vec3 b) {
  return vecMul(a, b);
}

// multiplies a vector and a scalar
inline Vec3 * (Vec3 a, Scalar b) {
  return vecMul(a, b);
}

// multiplies two vectors
inline Vec3 * (Vec3 a, Vec3 b) {
  return vecMul(a, b);
}

// multiplies this vector with a scalar
inline Vec3. *= (Scalar other) {
  this = this * other;
}

// multiplies this vector with another one
inline Vec3. *= (Vec3 other) {
  this = this * other;
}

// divides two vectors
inline Vec3 / (Vec3 a, Vec3 b) {
  if( Math_badDivisor( b.x ) )//Perf: check first to avoid building the report string
    Math_reportBadDivisor( b.x, "Vec3.divide: b.x" );
  if( Math_badDivisor( b.y ) )
    Math_reportBadDivisor( b.y, "Vec3.divide: b.y" );
  if( Math_badDivisor( b.z ) )
    Math_reportBadDivisor( b.z, "Vec3.divide: b.z" );
  return vecDiv(a, b);
}

// divides a vector by a scalar
inline Vec3 / (Vec3 a, Scalar b) {
  if( Math_badDivisor( b ) )//Perf: check first to avoid building the report string
    Math_reportBadDivisor( b, "Vec3.divide" );
  return vecDiv(a, b);
}

// divides this vector with a scalar
inline Vec3. /= (Scalar other) {
  this = this / other;
}

// divides this vector with another one
inline Vec3. /= (Vec3 other) {
  this = this / other;
}

// overload method for the add operator
inline Vec3 Vec3.add(Vec3 other) {
  return this + other;
}

// overload method for the subtract operator
inline Vec3 Vec3.subtract(Vec3 other) {
  return this - other;
}

// overload method for the multiply operator
inline Vec3 Vec3.multiply(Vec3 other) {
  return this * other;
}

// overload method for the divide operator
inline Vec3 Vec3.divide(Vec3 other) {
  return this / other;
}

// returns the product of this vector and a scalar
inline Vec3 Vec3.multiplyScalar(Scalar other) {
  return this * other;
}

// returns the division of this vector and a scalar
inline Vec3 Vec3.divideScalar(Scalar other) {
  return this / other;
}

// returns the negated version of this vector
inline Vec3 Vec3.negate() {
  return Vec3(-this.x, -this.y, -this.z);
}

// returns the inversed version of this vector
inline Vec3 Vec3.inverse() {
  if( Math_badDivisor( this.x ) )//Perf: check first to avoid building the report string
    Math_reportBadDivisor( this.x, "Vec3.inverse: this.x" );
  if( Math_badDivisor( this.y ) )
    Math_reportBadDivisor( this.y, "Vec3.inverse: this.y" );
  if( Math_badDivisor( this.z ) )
    Math_reportBadDivisor( this.z, "Vec3.inverse: this.z" );
  return Vec3(1.0/this.x, 1.0/this.y, 1.0/this.z);
}

// returns the inversed version of this vector
// by correcting bad components.
inline Vec3 Vec3.safeInverse() {
  Vec3 res;
  if( !Math_badDivisor( this.x ) )
    res.x = 1.0 / this.x;
  if( !Math_badDivisor( this.y ) )
    res.y = 1.0 / this.y;
  if( !Math_badDivisor( this.z ) )
    res.z = 1.0 / this.z;
  return res;
}

// returns the dot product of this vector and another one
inline Scalar Vec3.dot(Vec3 other) {
  return this.x * other.x + this.y * other.y + this.z * other.z;
}

// returns the cross product of this vector and another one
inline Vec3 Vec3.cross(in Vec3 other) {
  return Vec3(
    this.y * other.z - this.z * other.y,
    this.z * other.x - this.x * other.z,
    this.x * other.y - this.y * other.x
  );
}

// returns the squared length of this vector
inline Scalar Vec3.lengthSquared() {
  return this.dot(this);
}

// returns the length of this vector
inline Scalar Vec3.length() {
  return sqrt(this.lengthSquared());
}

// returns the unit vector of this one, throws and exception if almost zero length
inline Vec3 Vec3.unit() {
  Scalar n = this.length();
  if( Math_badDivisor( n ) )//Perf: check first to avoid building the report string
    Math_reportBadDivisor( n, "Vec3.unit" );
  return this / n;
}

// returns the unit vector of this one, with an arbitrary value if almost zero length
inline Vec3 Vec3.unit_safe() {
  Scalar n = this.length();
  if( !Math_badDivisor( n ) )//Perf: check first to avoid building the report string
    return this / n;
  else
    return Vec3(0,1,0);
}

// sets this vector to its unit vector
// and returns its previous length
inline Scalar Vec3.setUnit!() {
  Scalar n = this.length();
  if( Math_badDivisor( n ) )//Perf: check first to avoid building the report string
    Math_reportBadDivisor( n, "Vec3.setUnit" );
  this /= n;
  return n;
}

// clamps this vector per component by 
// a min and max vector
inline Vec3 Vec3.clamp(in Vec3 min, in Vec3 max) {
  return Vec3(
    (this.x < min.x ? min.x : (this.x > max.x ? max.x : this.x)),
    (this.y < min.y ? min.y : (this.y > max.y ? max.y : this.y)),
    (this.z < min.z ? min.z : (this.z > max.z ? max.z : this.z))
  );
}

// returns the angle (in radians) of this vector
// to another one
//Note: expects both vectors to be units (else use angleTo)
inline Scalar Vec3.unitsAngleTo(in Vec3 other) {
  Scalar acosAngle = Math_clamp(this.dot(other), -1.0, 1.0);
  return acos(acosAngle);
}

// returns the angle (in radians) of this vector
// to another one
inline Scalar Vec3.angleTo(in Vec3 other) {
  return this.unit().unitsAngleTo(other.unit());
}

// returns the distance of this vector to another one
inline Scalar Vec3.distanceTo(in Vec3 other) {
  return (this - other).length();
}

// linearly interpolates this vector with another one
// based on a scalar blend value (0.0 to 1.0)
inline Vec3 Vec3.linearInterpolate(in Vec3 other, in Scalar t) {
  return this + ((other - this) * t);
}

// returns the line ratio for the closest line location from point P
inline Scalar closestPointToLineRatio(in Vec3 P, in Vec3 lineP0, in Vec3 lineP1) {
  Vec3 v, w, Pb;
  Scalar c1, c2, b;
  v = lineP1 - lineP0;
  w = P - lineP0;

  c1 = w.dot(v);
  c2 = v.dot(v);
  if(c2 < 1e-10)//squared epsilon
    return 0.0;
  return c1 / c2;
}

// returns the distance of this vector to a line defined
// by two points on the line
inline Scalar distFromPointToLine(in Vec3 P, in Vec3 lineP0, in Vec3 lineP1) {
  Scalar closestRatio = closestPointToLineRatio(P, lineP0, lineP1);
  Vec3 Pb = lineP0 + closestRatio * (lineP1 - lineP0);
  return P.distanceTo(Pb);
}

// returns the closest point this position to a line segment defined
// by the start and end points
inline Vec3 closestSegmentPoint(in Vec3 P, in Vec3 segmentP0, in Vec3 segmentP1, io Scalar ratio) {
  Vec3 v, w, Pb;
  Scalar c1, c2, b;
  v = segmentP1 - segmentP0;
  w = P - segmentP0;

  c1 = w.dot(v);
  if (c1 <= 0) {
    ratio = 0;
    return segmentP0;
  }

  c2 = v.dot(v);
  if (c2 <= c1) {
    ratio = 1;
    return segmentP1;
  }

  ratio = c1 / c2;
  Pb = segmentP0 + ratio * v;
  return Pb;
}

// returns the distance of this position to a line segment defined
// by the start and end points
inline Scalar distFromPointToSegment(in Vec3 P, in Vec3 segmentP0, in Vec3 segmentP1) {
  Scalar ratio;
  return P.distanceTo(closestSegmentPoint(P, segmentP0, segmentP1, ratio));
}

// dist3D_Line_to_Line():
//    Input:  two 3D lines L1 and L2
//    Return: the shortest distance between L1 and L2
function Scalar distanceFromLineToLine(
  in Vec3 l1_p0, in Vec3 l1_p1,
  in Vec3 l2_p0, in Vec3 l2_p1,
  io Scalar l1_t, io Scalar l2_t
) {
  Vec3 u, v, w, dP;
  Scalar a, b, c, d, e, D;
  u = l1_p1 - l1_p0;
  v = l2_p1 - l2_p0;
  w = l1_p0 - l2_p0;
  a = u.dot(u); // always >= 0
  b = u.dot(v);
  c = v.dot(v); // always >= 0
  d = u.dot(w);
  e = v.dot(w);
  if(a == 0.0 && c == 0.0){
    return l1_p0.distanceTo(l2_p0);
  }
  if(a == 0.0){
    return distFromPointToLine(l1_p0, l2_p0, l2_p1);
  }
  if(c == 0.0){
    return distFromPointToLine(l2_p0, l1_p0, l1_p1);
  }
  D = a * c - b * b; // always >= 0

  // com_pute the line _parameters of the two closest points
  if (D < 0.001) {
    // the lines are almost parallel
    l1_t = 0.0;
    if (b > c) {
      // use the largest denominator
      l2_t = d / b;
    }else {
      l2_t = e / c;
    }
  }
  else {
    l1_t = (b * e - c * d) / D;
    l2_t = (a * e - b * d) / D;
  }
  // get the difference of the two closest points
  dP = w + (l1_t * u) - (l2_t * v);

  return dP.length(); // return the closest distance
}

///////////////////////////////////////////////////////////////////////////////////////
// Coplanarity test for 4 moving points. 
// These moving points could either be a point and a triangle, or 2 edges. 
// The returned fraction tells us if they will be coplanar within the next time step
// http://wscg.zcu.cz/wscg2007/Papers_2007/journal/D11-full.pdf
//
function Boolean testCoplanarity(
  in Vec3 x1,
  in Vec3 x2,
  in Vec3 x3,
  in Vec3 x4,
  in Vec3 v1,
  in Vec3 v2,
  in Vec3 v3,
  in Vec3 v4,
  io Scalar outFraction
){
  Vec3 x21, x31, x41, v21, v31, v41;

  x21 =  x2 - x1;
  x31 =  x3 - x1;
  x41 =  x4 - x1;

  v21 =  v2 - v1;
  v31 =  v3 - v1;
  v41 =  v4 - v1;

  // 
  Float64 coeff[4];
  Vec3 v31Xv41, x31Xv21, x41Xv31, x21Xx31;

  v31Xv41 = v31.cross(v41);
  x31Xv21 = x31.cross(v21);
  x41Xv31 = x41.cross(v31);
  x21Xx31 = x21.cross(x31);

  coeff[3] = v21.dot( v31Xv41 );
  coeff[2] = x21.dot( v31Xv41 ) - v41.dot( x31Xv21 ) - v21.dot( x41Xv31 );
  coeff[1] = v41.dot( x21Xx31 ) - x21.dot( x41Xv31 ) - x41.dot( x31Xv21 );
  coeff[0] = x41.dot( x21Xx31 );
  
  // The points will become coplanar in the next timestep if we find at least one
  // real root and it is between 0 and 1.
  Float64 roots[3];
  Integer numRoots = findCubicRoots( coeff, roots );
  if( numRoots > 0 ){
    // due to raounding error, we need to be a bit forgiving on the root finder. 
    Scalar fuzzyness = 0.001;
    if( roots[0] > (0.0 - fuzzyness) && roots[0] < (1.0 + fuzzyness) ){
      outFraction = Math_clamp(Scalar(roots[0]), 0.0, 1.0 );
      return true;
    }
    if( numRoots > 1 && roots[1] >= (0.0 - fuzzyness) && roots[1] < (1.0 + fuzzyness) ){
      outFraction = Math_clamp(Scalar(roots[1]), 0.0, 1.0 );
      return true; 
    }
    if( numRoots > 2 && roots[2] >= (0.0 - fuzzyness) && roots[2] < (1.0 + fuzzyness) ){
      outFraction = Math_clamp(Scalar(roots[2]), 0.0, 1.0 );
      return true;
    }
  }
  return false;
}


